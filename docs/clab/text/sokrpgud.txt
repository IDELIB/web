     H            Y/                                    1
     H*---------------------------------------------------------------*
     H* PROGRAM-ID    :    SOKRPGUD                                   *
     H* REMARKS       :    RPGソケットデーモン                      *
     H* AUTHOR        :    Y.IDE                                      *
     H* DATE-WRITEN   :    1999/03/27                                 *
     H* VERSION       :    01.00 ORIGINAL                             *
     H*---------------------------------------------------------------*
     C*
     E                    IP      1   1 15
     C*
     C*ソケットオープン--------------------------------------*
     C                     CALL 'SKOPEN'
     C                     PARM           SD      4
     C                     PARM           RC      1
     C*
     C           RC        IFEQ 'E'
     C                     GOTO TAG#E
     C                     ENDIF
     C*ソケットバインド--------------------------------------*
     C                     CALL 'SKBIND'
     C                     PARM           SD      4
     C                     PARM           RC      1
     C                     PARM 'ANY'     ANY    15
     C                     PARM '30001'   PORT    5
     C*--------------------------------------------------------*
     C* ＡＮＹもしくは、ＩＰアドレスのどちらかを指定する
     C* 以下はＩＰアドレス固定でバインドする例です。
     C*--------------------------------------------------------*
     C                     MOVE '0'       DUMMY   1
     C           DUMMY     IFEQ '1'
     C                     CALL 'SKBIND'
     C                     PARM           SD      4
     C                     PARM           RC      1
     C                     PARM           IP
     C                     PARM '30001'   PORT    5
     C                     ENDIF
     C*
     C           RC        IFEQ 'E'
     C                     GOTO TAG#E
     C                     ENDIF
     C*
     C           TAG#01    TAG
     C*ソケットリッスン--------------------------------------*
     C                     CALL 'SKLISN'
     C                     PARM           SD      4
     C                     PARM           RC      1
     C*
     C           RC        IFEQ 'E'
     C                     GOTO TAG#E
     C                     ENDIF
     C*ソケットアクセプト------------------------------------*
     C                     CALL 'SKACPT'
     C                     PARM           SD      4
     C                     PARM           RC      1
     C                     PARM           ND      4
     C*
     C           RC        IFEQ 'E'
     C                     GOTO TAG#E
     C                     ENDIF
     C*子プロセス生成----------------------------------------*
     C                     CALL 'SKSPWN'
     C                     PARM           ND      4
     C                     PARM           RC      1
     C                     PARM           PID     4
     C                     PARM 'SOKRPGUP'PGM    10
     C                     PARM 'CLAB'    LIB    10
     C           RC        IFEQ 'E'
     C                     GOTO TAG#E
     C                     ENDIF
     C*--------------------------------------------------------*
     C                     GOTO TAG#01
     C           TAG#E     TAG
     C                     SETON                     LR
     C                     RETRN
** SV SERVER IP ADDRESS
127.0.0.1

.
